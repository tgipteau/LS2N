/*************************************************************************
*
*	Système de réaction-diffusion modélisant
*	l'interaction entre la forêt mixte et
*	la forêt boréale.
*
*	Forêt mixte :
*	du1/dt = eta1*w1 - q1(u1, u2, x)
*	dw1/dt = d1*Delta w1 - beta1*w1 + alpha1*u1
*
*	Forêt boréale :
*	du2/dt = eta2*w2 - q2(u2, u1, x)
*	dw2/dt = d2*Delta w2 - beta2*w2 + alpha2*u2
*
*
**************************************************************************/


/* Paramètres */
string filename = "params.txt";
string key;
real n;
real[int] vecval(28);  // Tableau pour stocker les valeurs des paramètres

// Lecture du fichier et stockage des valeurs dans vecval
{
    ifstream file(filename);
    for (int i = 0; i < 28; i++) {
        file >> key >> n;
        cout << key << i <<" " << n << endl;
        vecval(i) = n;
    }
}

// Attribution des valeurs aux variables

// Forêt mixte
real alpha1 = vecval(0);
real beta1  = vecval(1);
real eta1   = vecval(2);
real a1     = vecval(3);
real b1     = vecval(4);
real d1     = vecval(5);

// Forêt boréale
real alpha2 = vecval(6);
real beta2  = vecval(7);
real eta2   = vecval(8);
real a2     = vecval(9);
real b2     = vecval(10);
real d2     = vecval(11);

// Compétition
real gamma = vecval(12);

// Discrétisation en temps
real dt   = vecval(13);
real dtau = vecval(14);

// Génération de nombre aléatoire
real p = vecval(15);

// Domaine
real L = vecval(16);
real l = vecval(17);

// Autres paramètres écologiques
real hb1 = vecval(18);
real hb2 = vecval(19);
real hm1 = vecval(20);
real hm2 = vecval(21);

// Paramètres supplémentaires
int frequency = vecval(22);
real minree = vecval(23);
real maxree = vecval(24);
real minI   = vecval(25);
real maxI   = vecval(26);

real tmax = vecval(27);

cout << "tmax" <<" "<< tmax << endl;
cout << "tmax/dt" <<" "<< tmax/dt << endl;

// Construction du maillage
border C(t=0,2*pi){x=(L/2)*(cos(t))+(L/2); y=(l/2)*(sin(t))+(l/2);}
mesh Th = buildmesh(C(80));

fespace Vh(Th, P1);


func int Bernoulli(real p)
{
	real DE;
	DE = randreal1();
	return (DE<p);
}


/* Reaction */

// Taux de mortalité pour la forêt mixte
func real c1(real x, real y)
{
	if ((10<y) && (y<60)){
		return hm1;
	}
	else {
		return hm2;
	}
}

// Taux de mortalité pour la forêt boréale
func real c2(real x, real y)
{
	if ((40<y) && (y<70)){
		return hb1;
	}
	else {
		return hb2;
	}

}

func real[int] fct(real[int] X, real t)
{
	// X[0] -> u1
	// X[1] -> w1
	// X[2] -> u2
	// X[3] -> w2

	real[int] dX(4);
	dX = 1.0;
	// Forêt mixte
    	dX[0] = eta1*X[1] - X[0]*(a1*(X[0]-b1)*(X[0]-b1)+c1(x, y)) + gamma*X[0]*X[2];
	dX[1] = -beta1*X[1] + alpha1*X[0];
	// Forêt boréale
    	dX[2] = eta2*X[3] - X[2]*(a2*(X[2]-b2)*(X[2]-b2)+c2(x, y)) - gamma*X[0]*X[2];
	dX[3] = -beta2*X[3] + alpha2*X[2];
    	return dX;
}




/* Runge-Kutta */

func real[int] RK4(real[int] U, real t)
{
	real[int] u2(4), u3(4), u4(4);
	real[int] p1(4), p2(4), p3(4), p4(4);
	real t2, tnew;
	real[int] unew(4);
	unew = 1.0;

    	p1 = fct(U, t);
    	t2 = t + 0.5*dt;
    	u2 = U + 0.5*dt*p1;
    	p2 = fct(u2, t2);
    	u3 = U + 0.5*dt*p2;
    	p3 = fct(u3, t2);
    	tnew = t + dt;
    	u4 = U + dt*p3;
    	p4 = fct(u4, tnew);
    	unew = 1.0*p1;
	unew = unew + 2.0*p2;
	unew = unew + 2.0*p3;
	unew = unew + 1.0*p4;
	unew = U + dt*1./6.*unew;
    	return unew;
}






/* Diffusion */

Vh u1, u2, w1new, w1old, w2new, w2old, phi1, phi2;

problem Diffusionw1(w1new, phi1) = int2d(Th)( w1new*phi1 + d1*dtau*(dx(w1new)* dx(phi1) + dy(w1new)* dy(phi1))) + int2d(Th) ( -w1old*phi1 );
problem Diffusionw2(w2new, phi2) = int2d(Th)( w2new*phi2 + d2*dtau*(dx(w2new)* dx(phi2) + dy(w2new)* dy(phi2))) + int2d(Th) ( -w2old*phi2 );

real t = 0.0;

cout << "GO !!!!\n";

/* Initial condition */

// Forêt mixte
func real u10(real x, real y)
{
	real res;
	res = 3+0.1*randreal1();//y*4/l;
	return res;
}

func real w10(real x, real y)
{
	real res;
	res = 3+0.1*randreal1();//y*4/l;
	return res;
}

// Forêt boréale
func real u20(real x, real y)
{
	real res;
	res = 3+0.1*randreal1();//y*4/l;
	return res;
}

func real w20(real x, real y)
{
	real res;
	res = 3+0.1*randreal1();//y*4/l;
	return res;
}

u1 = u10(x, y);
w1old = w10(x, y);
u2 = u20(x, y);
w2old = w20(x, y);




/* splitting scheme */


for (int m=0; m<tmax/dt; m++)
{

    /*
    // PLOTS
	plot(u1, WindowIndex=1, cmm="MIXTE : t="+ t + ", u1: min=" + u1[].min + ", max=" + u1[].max, wait=false, fill=true);//, dim=3);
	plot(u2, WindowIndex=2, cmm="BOREALE : t="+ t + ", u2: min=" + u2[].min + ", max=" + u2[].max, wait=false, fill=true);//, dim=3);
    */

	ofstream f1("u-"+m+".dat");

    	for (int i=0;i<Th.nt;i++){
            	for (int j=0; j<3; j++){
                    	f1 << Th[i][j].x << "\t" << Th[i][j].y << "\t" << u1[][Vh(i,j)] << "\t" << u2[][Vh(i,j)] << endl;
            	}
	}



	// Diffusion for half-step
	Diffusionw1;
	Diffusionw2;
	w1old = w1new;
	w2old = w2new;
	t = t + dtau;

	// Reaction for one step
	func real[int] reaction(real x, real y)
		{
			real[int] U(4);
			U[0] = u1;
			U[1] = w1old;
			U[2] = u2;
			U[3] = w2old;
			U = RK4(U, t);
			return U;
		}

	u1 = reaction(x, y)[0];
	w1old = reaction(x, y)[1];
	u2 = reaction(x, y)[2];
	w2old = reaction(x, y)[3];
	t = t + dt;

	// Diffusion for half-step
	Diffusionw1;
	Diffusionw2;
	w1old = w1new;
	w2old = w2new;
	t = t + dtau;


	// Evénement extrême de type feu localisé
			if (m%frequency == 0){		// On définit une fréquence temporelle de probabilité d'événement extrême
				if (Bernoulli(p)==1){
					cout << "//////////////////////" << endl;
					cout << "// FEU DE FORET !!! //" << endl;
					cout << "//////////////////////" << endl;
					real xee = -1.0 + 2.0*randreal1();
					real yee = -1.0 + 2.0*randreal1();
					xee = xee/sqrt(2);
					yee = yee/sqrt(2);
					xee = (L/2.0) + (L/2.0)*xee;
					yee = (l/2.0) + (l/2.0)*yee;
					/* Remarque :

					   Pour envoyer le carré [-1, 1] x [-1, 1] sur le disque unité, on peut
					   utiliser la transformation

					   	X = x * sqrt(1 - y * y / 2),
						Y = y * sqrt(1 - x * x / 2),

					   après avoir généré aléatoirement x et y :
					   	x = -1 + 2*random(),
						y = -1 + 2*random().

					*/


					cout << "Feu en (" << xee << ", " << yee << ")." << endl;
					real ree = minree + randreal1() * (maxree - minree);	//radius extreme event
					real intensity = minI + randreal1() * (maxI - minI); // intensity extreme event


					// enregistrement des infos sur le feu dans le fichier

					ofstream ffires ("fires.dat", append);
					ffires << m << "\t" << xee << "\t" << yee << "\t" << ree << "\t" << intensity << endl;


					func real u1ee(real x, real y){
						real res;
						if ((x-xee)*(x-xee)+(y-yee)*(y-yee)<ree*ree){
							res = u1(x, y)*(1-intensity);
						}
						else{
							res = u1(x,y);
						}
						return res;
					}

					func real w1ee(real x, real y){
						real res;
						if ((x-xee)*(x-xee)+(y-yee)*(y-yee)<ree*ree){
							res = w1old(x, y)*(1-intensity);
						}
						else{
							res = w1old(x,y);
						}
						return res;
					}
					u1 = u1ee(x, y);
					w1old = w1ee(x, y);

					func real u2ee(real x, real y){
						real res;
						if ((x-xee)*(x-xee)+(y-yee)*(y-yee)<ree){
							res = u2(x, y)*(1-intensity);
						}
						else{
							res = u2(x,y);
						}
						return res;
					}
					func real w2ee(real x, real y){
						real res;
						if ((x-xee)*(x-xee)+(y-yee)*(y-yee)<ree){
							res = w2old(x, y)*(1-intensity);
						}
						else{
							res = w2old(x,y);
						}
						return res;
					}
					u2 = u2ee(x, y);
					w2old = w2ee(x, y);
				}
				else {
					cout << "=======================" << endl;
					cout << "= PAS DE FEU DE FORET =" << endl;
					cout << "=======================" << endl;
				}

			}
}

{
// fichier qui indique la fin du programme (uniquement pour implémentation en subprocess python)
ofstream ffires ("EOP");
}



